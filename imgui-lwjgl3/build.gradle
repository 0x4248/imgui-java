plugins {
    id 'java'
    id 'checkstyle'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

sourceCompatibility = 1.8

ext {
    lwjglVersion = '3.2.3'
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation 'org.lwjgl:lwjgl'
    implementation 'org.lwjgl:lwjgl-glfw'
    implementation 'org.lwjgl:lwjgl-opengl'

    implementation project(':imgui-binding')
}

task startExample(type: JavaExec, dependsOn: build) {
    main = 'ImGuiGlfwExample'
    classpath = sourceSets.test.runtimeClasspath
    jvmArgs = ['-Djava.library.path=../bin', '-Dimgui.library.name=imgui-java64', '-Dorg.lwjgl.librarypath=./lwjgl-libs']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'io.imgui.java'
            artifactId 'lwjgl3'
            artifact sourcesJar
            artifact javadocJar
            version property('version')
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    override = true
    pkg {
        repo = 'io.imgui.java'
        name = 'lwjgl3'
        userOrg = user
        desc = 'Backend implementation for imgui-java based on LWJGL3'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/SpaiR/imgui-java'
        labels = ['dear-imgui', 'java', 'lwjgl3']
        githubRepo = 'SpaiR/imgui-java'
        publicDownloadNumbers = true
    }
}
