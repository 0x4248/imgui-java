plugins {
    id 'java'
    id 'checkstyle'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

sourceCompatibility = 1.8

ext {
    lwjglVersion = '3.2.3'
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation 'org.lwjgl:lwjgl'
    implementation 'org.lwjgl:lwjgl-glfw'
    implementation 'org.lwjgl:lwjgl-opengl'

    implementation project(':imgui-binding')
}



task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'imgui-java'
            artifact sourcesJar
            artifactId 'lwjgl3'
            version property('version')
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    override = true
    pkg {
        repo = 'imgui-java'
        name = 'lwjgl3'
        userOrg = user
        desc = 'Backend implementation for imgui-java based on LWJGL3'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/SpaiR/imgui-java'
        labels = ['dear-imgui', 'java', 'lwjgl3']
        githubRepo = 'SpaiR/imgui-java'
        publicDownloadNumbers = true
    }
}
