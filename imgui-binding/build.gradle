import imgui.generate.GenerateLibs

plugins {
    id 'java'
    id 'checkstyle'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

sourceCompatibility = 1.8

assemble.dependsOn clean
task generateLibs(type: GenerateLibs, dependsOn: assemble)

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'io.imgui.java'
            artifactId 'binding'
            artifact sourcesJar
            artifact javadocJar
            version property('version')
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    override = true
    pkg {
        repo = 'io.imgui.java'
        name = 'binding'
        userOrg = user
        desc = 'A handcrafted/generated Java binding for the Dear-ImGui'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/SpaiR/imgui-java'
        issueTrackerUrl  = 'https://github.com/SpaiR/imgui-java/issues'
        vcsUrl = 'https://github.com/SpaiR/imgui-java.git'
        labels = ['dear-imgui', 'java', 'jni']
        githubRepo = 'SpaiR/imgui-java'
        publicDownloadNumbers = true
    }
}
